S -> DeclVar PF VAR DeclVar INICIO Stat FIM
Tipo -> INTEIRO
Tipo -> REAL
TipoFuncao -> INTEIRO
TipoFuncao -> REAL
TipoFuncao -> NULO
ListaId -> ID ; ListaId
ListaId -> ID 
ListaLeitura -> ID ; ListaLeitura
ListaLeitura -> ID [ ExprArit ] ; ListaLeitura
ListaLeitura -> ID .
ListaLeitura -> ID [ ExprArit ] . 
ListaVarAntes -> ListaVar
ListaVarAntes -> ''
ListaVar ->  VET Tipo : ID ; ListaVar
ListaVar ->  Tipo : ID ; ListaVar
ListaVar ->  VET Tipo : ID
ListaVar ->  Tipo : ID
ListaFuncAntes -> ListaFunc
ListaFuncAntes -> ''
ListaFunc -> ExprArit ; ListaFunc
ListaFunc -> ExprArit
ListaEscreve -> ExprArit ; ListaEscreve
ListaEscreve -> LITERAL ; ListaEscreve
ListaEscreve -> ExprArit .
ListaEscreve -> LITERAL .
DeclVar -> Tipo : ListaId . DeclVar
DeclVar -> VET Tipo : ID NUM . DeclVar
DeclVar -> ''
ExprArit -> Nivel1
Nivel1 -> Nivel2 + Nivel1
Nivel1 -> Nivel2 - Nivel1
Nivel1 -> Nivel2
Nivel2 -> Nivel3 * Nivel2
Nivel2 -> Nivel3 / Nivel2
Nivel2 -> Nivel3 % Nivel2
Nivel2 -> Nivel3
Nivel3 -> Nivel4 ** Nivel3
Nivel3 -> Nivel4
Nivel4 -> ( ExprArit )
Nivel4 -> ID
Nivel4 -> ID ( ListaFuncAntes ) 
Nivel4 -> NUM
Nivel4 -> ID [ ExprArit ]
Nivel4 -> ( ExprArit )
Nivel4 -> - ID
Nivel4 -> - ID ( ListaFuncAntes ) 
Nivel4 -> - NUM
Nivel4 -> - ID [ ExprArit ]
Atrib -> ID <- ExprArit .
Atrib -> ID [ ExprArit ] <- ExprArit .
ExpRel -> ExprArit > ExprArit
ExpRel -> ExprArit < ExprArit
ExpRel -> ExprArit <= ExprArit
ExpRel -> ExprArit >= ExprArit
ExpRel -> ExprArit = ExprArit
ExpRel -> ExprArit <> ExprArit
ExpRelC -> Nivell1
Nivell1 -> Nivell2 | Nivell1
Nivell1 -> Nivell2
Nivell2 -> Nivell3 & Nivell2
Nivell2 -> Nivell3
Nivell3 -> ( ExpRelC )
Nivell3 -> ! ( ExpRelC )
Nivell3 -> ExpRel
Nivell3 -> ! ExpRel
Leitura -> LEIA ListaLeitura
Escrita -> ESCREVA ListaEscreve
Stat -> Leitura Stat
Stat -> DesvioCondicional Stat
Stat -> RepeticaoEnquanto Stat
Stat -> RepeticaoPara Stat
Stat -> Escrita Stat
Stat -> Atrib Stat
Stat -> ChamadaFunc Stat
Stat -> PARE . Stat
Stat -> RECEBA . Stat
Stat -> RECEBA ( ExprArit ) . Stat 
Stat -> ''
DesvioCondicional -> SE ExpRelC INICIO Stat FIM
DesvioCondicional -> SE ExpRelC INICIO Stat FIM SENAO INICIO Stat FIM 
RepeticaoEnquanto -> ENQUANTO ExpRelC INICIO Stat FIM
RepeticaoPara -> PARA ID DE ExprArit ATEH ExprArit INICIO Stat FIM
PF -> FUNCAO ID ( ListaVarAntes ) : TipoFuncao . PF
PF -> GeraFunc
GeraFunc -> ''
GeraFunc -> FUNCAO ID ( ListaVarAntes ) : TipoFuncao VAR DeclVar INICIO Stat FIM GeraFunc
ChamadaFunc -> ID ( ListaFuncAntes ) . 